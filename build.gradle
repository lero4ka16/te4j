plugins {
    id 'java'

    id 'maven-publish'
    id 'signing'
}

group 'com.github.lero4ka16'
description 'Fast, lightweight and easy template engine'
version findProperty('github.tag') ?: ''
archivesBaseName = 'te4j'

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.immutables:value:2.8.2'
    compileOnly 'org.immutables:value:2.8.2'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = project.name
                description = project.description

                url = 'https://github.com/lero4ka16/te4j'

                organization {
                    name = 'com.github.lero4ka16'
                    url = 'https://github.com/lero4ka16'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/lero4ka16/te4j/issues'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    url = 'https://github.com/lero4ka16/te4j'
                    connection = 'scm:https://github.com/lero4ka16/te4j.git'
                    developerConnection = 'scm:git://github.com/lero4ka16/te4j.git'
                }

                developers {
                    developer {
                        id = 'lero4ka16'
                        name = 'Lero4ka16'
                        email = 'lero4ka6916@gmail.com'
                    }
                }
            }

            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }

    repositories {
        // github packages
        maven {
            url = "https://maven.pkg.github.com/lero4ka16/te4j"

            credentials {
                username = findProperty('gpr.user')
                password = findProperty('gpr.password')
            }
        }

        // maven central
        if (!project.version.endsWith("-SNAPSHOT")) {
            maven {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"

                credentials {
                    username = findProperty('sonatype.user')
                    password = findProperty('sonatype.password')
                }
            }
        }
    }
}


signing {
    sign publishing.publications
}


java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.encoding = 'UTF-8'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true

        exceptionFormat "short"
        events("started", "skipped", "failed")
    }
}